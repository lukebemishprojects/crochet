plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.gradleup.shadow'
}

group = 'dev.lukebemish.crochet'
// TODO: managed versioning
version = '0.1.0'

repositories {
    mavenCentral()
    maven {
        url = 'https://maven.fabricmc.net/'
    }
}

dependencies {
    implementation 'net.fabricmc:tiny-remapper:0.9.0'
    implementation 'info.picocli:picocli:4.7.6'
    implementation 'net.neoforged:srgutils:1.0.9'
    implementation 'net.fabricmc:access-widener:2.1.0'
    implementation 'com.google.code.gson:gson:2.11.0'
    annotationProcessor 'info.picocli:picocli-codegen:4.7.6'

    // TODO: conventions
    compileOnly 'org.jetbrains:annotations:23.1.0'
}

compileJava {
    options.compilerArgs += ["-Aproject=${project.group}/${project.name}"]
}

jar {
    manifest {
        attributes([
            'Main-Class' : 'dev.lukebemish.crochet.tools.Main',
            'Implementation-Version' : project.version as String
        ])
    }
    archiveClassifier.set('slim')
}

shadowJar {
    manifest {
        attributes([
            'Main-Class' : 'dev.lukebemish.crochet.tools.Main',
            'Implementation-Version' : project.version as String
        ])
    }
    archiveClassifier.set('')
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}
