plugins {
    id 'java-library'
    id 'maven-publish'
    id 'dev.lukebemish.crochet'
}

repositories {
    if (project.useLocalMavenForTesting) {
        mavenLocal()
    }

    mavenCentral()
    maven {
        name = "ParchmentMC"
        url = uri("https://maven.parchmentmc.org/")
    }
}

java.withSourcesJar()

group = 'dev.lukebemish.crochet'
version = '1.0.0'

crochet {
    fabric('fabric') {
        minecraft = '1.21.1'

        dependencies {
            loader 'net.fabricmc:fabric-loader:0.16.0'
            mappings = chained {
                add official()
                add(artifact 'org.parchmentmc.data:parchment-1.21:2024.07.28@zip')
            }
        }

        forFeature(project.sourceSets.main) {
            modImplementation project(':subproject')
        }
    }

    fabric('fabricYarn') {
        minecraft = '1.21.1'

        dependencies {
            loader 'net.fabricmc:fabric-loader:0.16.0'
            mappings chained {
                add intermediary()
                add(artifact 'net.fabricmc:yarn:1.21.1+build.3:v2')
            }
        }
    }

    vanilla('vanilla') {
        minecraft = '1.21.1'
    }

    runs {
        vanillaClient {
            client installations.vanilla
        }
        fabricClient {
            client installations.fabric
            implementation project(':')
        }
        fabricYarnClient {
            client installations.fabricYarn
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}
