plugins {
    id 'java-library'
    id 'maven-publish'
    id 'dev.lukebemish.crochet'
}

repositories {
    if (project.useLocalMavenForTesting) {
        mavenLocal()
    }

    mavenCentral()
    maven {
        name = "ParchmentMC"
        url = uri("https://maven.parchmentmc.org/")
    }
    maven {
        name = 'Staging'
        url = 'https://maven.lukebemish.dev/staging/'
    }
}

java.withSourcesJar()

group = 'dev.lukebemish.crochet'
version = '1.0.0'

jar {
    manifest {
        attributes(
            'Fabric-Loom-Split-Environment': 'true'
        )
    }
}

configurations {
    canBeResolved {
        attributes {
            attribute dev.lukebemish.crochet.internal.CrochetProjectPlugin.NEO_DISTRIBUTION_ATTRIBUTE, 'client'
            attribute Usage.USAGE_ATTRIBUTE, objects.named(Usage, 'java-api')
            attribute Category.CATEGORY_ATTRIBUTE, objects.named(Category, 'library')
        }
    }
}
dependencies {
    canBeResolved 'net.fabricmc:fabric-loader:0.16.9'
}

crochet {
    fabricSplitSourceSets('main') {
        installation {
            minecraft = '1.21.4'

            dependencies {
                loader 'net.fabricmc:fabric-loader:0.16.9'
                mappings = chained {
                    add official()
                    add(artifact 'org.parchmentmc.data:parchment-1.21.4:2025.02.16@zip')
                }
            }
        }

        baseFeature = ''

        common('common') {
            dependencies {
                modImplementation project(':subproject')
            }
        }
        server('server')
        client('client')
        joined('main')
    }

    features {
        create('yarn') {
            disablePublication()
        }
    }

    externalFabricInstallation('fabricYarn') {
        consume(sharedInstallations.yarn)

        forFeature(sourceSets.yarn)
    }

    neoFormInstallation('neoForm') {
        dependencies {
            neoForm 'net.neoforged:neoform:1.21.3-20241023.131943'
        }

        share('neoform')
    }

    runs {
        fabricClient {
            client splitSourceSets.main.client
            implementation project(':')
        }
        fabricServer {
            server splitSourceSets.main.server
            implementation project(':')
        }
        fabricYarnClient {
            client installations.fabricYarn
            implementation(project(':')) {
                capabilities {
                    requireFeature('yarn')
                }
            }
        }
        neoFormClient {
            client installations.neoForm
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}
