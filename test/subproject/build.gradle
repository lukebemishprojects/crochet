plugins {
    id 'java-library'
    id 'maven-publish'
    id 'dev.lukebemish.crochet'
}

repositories {
    if (project.useLocalMavenForTesting) {
        mavenLocal()
    }

    mavenCentral()
    maven {
        name = "ParchmentMC"
        url = uri("https://maven.parchmentmc.org/")
    }
    maven {
        name = 'Staging'
        url = 'https://maven.lukebemish.dev/staging/'
    }
}

java.withSourcesJar()

group = 'dev.lukebemish.crochet.crochet-test'
version = '1.0.0'

crochet {
    fabricInstallation('fabric') {
        minecraft = '1.21.4'

        dependencies {
            loader 'net.fabricmc:fabric-loader:0.16.9'
            mappings = chained {
                add official()
                add(artifact 'org.parchmentmc.data:parchment-1.21.4:2025.02.16@zip')
            }
            publishInjectedInterfaces file('interfaces.json')
        }

        forFeature(project.sourceSets.main) {
            modApi 'net.fabricmc.fabric-api:fabric-api:0.114.1+1.21.4'
        }
    }
    runs {
        fabricClient {
            client installations.fabric
            implementation project(':subproject')
        }
    }
}

dependencies {
    api project(':no-crochet')
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}
